apply plugin: 'java'
apply plugin: 'osgi'
apply plugin: 'idea'

repositories {
  mavenCentral()
}

configurations {
  packagedJars
  compile.extendsFrom packagedJars
}

dependencies {
  packagedJars 'org.ow2.asm:asm-all:4.2', 'org.yaml:snakeyaml:1.13'
  compile 'org.osgi:org.osgi.core:4.3.1'
  testCompile 'org.apache.felix:org.apache.felix.framework:4.2.1',
              'org.ops4j.pax.exam:pax-exam-container-native:3.4.0',
              'org.ops4j.pax.exam:pax-exam-junit4:3.4.0',
              "javax.inject:javax.inject:1"
}

jar {
  manifest {
    version = '1.0.0'
    symbolicName = 'org.simple.monitor'
    instruction 'DynamicImport-Package', '*'
    instruction 'Import-Package', 'org.osgi.framework, org.osgi.framework.wiring, org.osgi.framework.hooks.weaving'
    instruction 'Export-Package', 'org.simple.monitor'
    instruction 'Bundle-Activator', 'org.simple.monitor.osgi.Activator'
    instruction 'Bundle-RequiredExecutionEnvironment', 'JavaSE-1.7'
  }
  from configurations.packagedJars.findAll().collect { zipTree(it) }
}

task copyToLibs(type: Copy, dependsOn: build) {
    from configurations.runtime
    into "$buildDir/libs"
}
